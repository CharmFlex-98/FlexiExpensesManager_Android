plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    alias(libs.plugins.kotlin.kapt)
    alias(libs.plugins.google.services)
    alias(libs.plugins.google.firebase.crashlytics.plugin)
    alias(libs.plugins.kotlin.serialization.plugin)
}

android {
    namespace 'com.charmflex.flexiexpensesmanager'
    compileSdk 34

    defaultConfig {
        applicationId "com.charmflex.flexiexpensesmanager"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            debuggable true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions = ["dimension"]

    productFlavors {
        dev {
            resValue "string", "app_name", "FlexiExpensesManagerDev"
            dimension("dimension")
            applicationIdSuffix(".dev")
            buildConfigField("String", "BASE_URL", "\"http://10.0.2.2:3000\"")
        }

        mock {
            dimension("dimension")
            applicationIdSuffix(".mock")
            buildConfigField("String", "BASE_URL", "\"http://localhost:56789\"")
        }

        prod {
            resValue "string", "app_name", "Flexi Expenses Manager"
            dimension "dimension"
            buildConfigField("String", "BASE_URL", "\"https://app.charmflex.com\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.composeCompilerVersion.get()
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.0')
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3:1.2.0-alpha11'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    implementation(libs.kotlin.serialization)
    implementation(libs.squareup.moshi)
    kapt(libs.moshi.kotlin.codegen)

    // Compose Navigation
    implementation(libs.compose.navigation)

    // Chart library
    implementation(libs.aay.chart)

    // Compose Calendar
    implementation(libs.compose.calendar)

//    // Activity
//    implementation(libs.activity.ktx)
//
//    // Fragment
//    implementation(libs.fragment.ktx)

    // ViewModel
    implementation(libs.lifecycle.viewModel.ktx)
    implementation(libs.lifecycle.viewModel.compose)

    // Accompanist
    implementation(libs.google.accompanist)

    // Room
    implementation(libs.android.room)
    kapt(libs.android.room.compiler)
    implementation(libs.android.room.ktx)

    // Network
    implementation(platform(libs.square.okhttp3.bom))
    implementation(libs.square.okhttp3.loggingInterceptor)
    implementation(libs.square.okhttp.tls)
    implementation(libs.square.okhttp3)
    implementation(libs.square.retrofit)
    implementation(libs.square.retrofit.moshiConverter)

    // Excel
    implementation(libs.apache.poi)
    implementation(libs.apache.poi.ooxml)

    // Dagger
    implementation(libs.google.dagger)
    kapt(libs.google.daggerAP)

    // Lottie
    implementation(libs.airbnb.lottie.compose)

    // Dialog
    implementation(libs.skydoves.balloon)
    implementation(libs.compose.dialog.state)
    implementation(libs.compose.dialog.calendar)
    implementation(libs.compose.dialog.clock)
    implementation(libs.compose.dialog.state)
    implementation(libs.coil)

    // firebase
    implementation(platform(libs.google.firebase.bom))
    implementation(libs.google.firebase.crashlytics)
    implementation(libs.google.firebase.analytics)

    // security
    implementation(libs.androidx.security.crypto)
}